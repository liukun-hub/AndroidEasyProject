apply plugin: 'com.android.application'

//android 配置
def config = rootProject.ext.android
//依赖
def librarys = rootProject.ext.dependencies
//签名文件的配置文件
def keystorePropertiesFile = rootProject.file("sign.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    compileSdkVersion config.compileSdkVersion

    defaultConfig {
        // 无痛修改包名：https://www.jianshu.com/p/17327e191d2e
        applicationId "com.liukun.androideasyproject"
        minSdkVersion config.minSdkVersion
        targetSdkVersion config.targetSdkVersion
        versionCode config.versinCode
        versionName config.versionName

        testInstrumentationRunner config.testInstrumentationRunner

        // 阿里巴巴矢量图库：https://www.iconfont.cn/
        vectorDrawables.useSupportLibrary = true
    }

    signingConfigs {
        signconfig {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }


    buildTypes {
        debug {
            debuggable true
            jniDebuggable true
            // 移除无用的资源文件
            shrinkResources false
            // ZipAlign 优化
            zipAlignEnabled false
            // 设置混淆
            minifyEnabled false
            // 开发环境下的 BuglyId
            buildConfigField "String", "BUGLY_ID", "\"请自行替换 Bugly 上面的 AppID\""
            signingConfig signingConfigs.signconfig
        }

        release {
            jniDebuggable false
            // 移除无用的资源文件
            shrinkResources true
            // ZipAlign 优化
            zipAlignEnabled true
            // 设置混淆
            minifyEnabled true
            // 正式环境下的 BuglyId
            buildConfigField "String", "BUGLY_ID", "\"请自行替换 Bugly 上面的 AppID\""
            signingConfig signingConfigs.signconfig
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    // 默认渠道名
    flavorDimensions "default"
    // 友盟多渠道打包
    android.productFlavors {
        tencent {}  // 应用宝
        baidu {}    // 百度
        xiaomi {}   // 小米
        huawei {}   // 华为

        productFlavors.all { flavor ->
            flavor.manifestPlaceholders = [channel: name]
        }
    }

    // 执行配置
    applicationVariants.all { variant ->

        // Apk 输出配置
        variant.outputs.all { output ->
            outputFileName = rootProject.getName() + '_v' + variant.versionName + '_' + variant.buildType.name
            if (variant.buildType.name == buildTypes.release.getName()) {
                outputFileName += '_' + new Date().format("yyyyMMdd")
            }
            outputFileName += '.apk'
        }
    }
}

dependencies {
    implementation librarys.libs
    implementation librarys.appcompat
    implementation librarys.constraintlayout
    implementation librarys.junit
    implementation librarys.test_ext
    implementation librarys.espresso_core
    implementation librarys.recyclerview

    // 基础库
    implementation project(':base')
    // Dex 分包，解决 64k 方法问题
    //implementation 'androidx.multidex:multidex:2.0.1'

    // 网络请求框架：https://github.com/getActivity/EasyHttp

//    // OkHttp 框架：https://github.com/square/okhttp
//    implementation 'com.squareup.okhttp3:okhttp:4.6.0'

    // 权限请求框架：https://github.com/getActivity/XXPermissions
    implementation 'com.hjq:xxpermissions:6.2'

    // 标题栏框架：https://github.com/getActivity/TitleBar
    implementation 'com.hjq:titlebar:6.0'

    // 吐司框架：https://github.com/getActivity/ToastUtils
    implementation 'com.hjq:toast:8.0'

    // JSON 解析框架：https://github.com/google/gson
    implementation 'com.google.code.gson:gson:2.8.6'

    // AOP 插件库：https://mvnrepository.com/artifact/org.aspectj/aspectjrt
    implementation 'org.aspectj:aspectjrt:1.9.5'

    // ButterKnife 注解库：https://github.com/JakeWharton/butterknife
    if (project.hasProperty('android.injected.invoked.from.ide')) {
        // 开发时使用运行时注解，提升 IDE 编译速度
        implementation 'com.jakewharton:butterknife-reflect:10.2.0'
    } else {
        // 打包时使用编译时注解，提升 APP 运行速度
        implementation 'com.jakewharton:butterknife:10.2.0'
        annotationProcessor 'com.jakewharton:butterknife-compiler:10.2.0'
    }

    // 图片加载框架：https://github.com/bumptech/glide
    implementation 'com.github.bumptech.glide:glide:4.11.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'com.google.android.material:material:1.1.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.11.0'

    // 状态栏沉浸：https://github.com/gyf-dev/ImmersionBar
    implementation 'com.gyf.immersionbar:immersionbar:3.0.0'
// 手势 ImageView：https://github.com/chrisbanes/PhotoView
    implementation 'com.github.chrisbanes:PhotoView:2.3.0'

    // Bugly 异常捕捉：https://bugly.qq.com/docs/user-guide/instruction-manual-android/?v=20190418140644
//    implementation 'com.tencent.bugly:crashreport:3.2.2'
//    implementation 'com.tencent.bugly:nativecrashreport:3.7.1'

    // 动画解析库：https://github.com/airbnb/lottie-android
    // 动画资源：https://lottiefiles.com/
    implementation 'com.airbnb.android:lottie:3.2.0'

    // 上拉刷新下拉加载：https://github.com/scwang90/SmartRefreshLayout
    implementation 'com.scwang.smartrefresh:SmartRefreshLayout:1.1.3'
    // SmartRefreshLayout 刷新样式集合库
    implementation 'com.scwang.smartrefresh:SmartRefreshHeader:1.1.0'
    // 侧滑框架：https://github.com/luckybilly/SmartSwipe
// implementation 'com.billy.android:smart-swipe:1.1.2'
// implementation 'com.billy.android:smart-swipe-x:1.1.0'

    // ViewPager 指示器：https://github.com/romandanylyk/PageIndicatorView
    implementation 'com.romandanylyk:pageindicatorview:1.0.3'
    //https://github.com/CymChad/BaseRecyclerViewAdapterHelper
    implementation 'com.github.CymChad:BaseRecyclerViewAdapterHelper:3.0.4'
    //https://github.com/Tencent/MMKV/blob/master
    implementation 'com.tencent:mmkv-static:1.1.2'

    implementation 'org.greenrobot:eventbus:3.2.0'
    // 国际化：https://github.com/getActivity/MultiLanguages
    // 悬浮窗：https://github.com/getActivity/XToast
    // 工具类：https://github.com/Blankj/AndroidUtilCode
    // 轮播图：https://github.com/bingoogolapple/BGABanner-Android
    // 二维码：https://github.com/bingoogolapple/BGAQRCode-Android
    // 屏幕适配：https://github.com/JessYanCoding/AndroidAutoSize
    // 跑马灯：https://github.com/sunfusheng/MarqueeView
    // 第三方支付：https://github.com/Cuieney/RxPay
    // Log 打印：https://github.com/JakeWharton/timber
    // 图片裁剪：https://github.com/Yalantis/uCrop
    // 图片压缩：https://github.com/Curzibn/Luban
    // 对象存储：https://github.com/leavesC/DoKV
    // 数据注入：https://github.com/JumeiRdGroup/Parceler
    implementation 'me.jessyan:autosize:1.2.1'
}